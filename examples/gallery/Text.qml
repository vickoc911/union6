// SPDX-License-Identifier: LGPL-2.1-only OR LGPL-3.0-only OR LicenseRef-KDE-Accepted-LGPL
// SPDX-FileCopyrightText: 2025 Arjen Hiemstra <ahiemstra@heimr.nl>

import QtQuick
import QtQuick.Layouts
import QtQuick.Controls as Controls
import org.kde.kirigami as Kirigami

Kirigami.Page {
    title: "Text QtQuick.Controls"

    Kirigami.ColumnView.interactiveResizeEnabled: true
    Kirigami.ColumnView.minimumWidth: Kirigami.Units.gridUnit * 10
    Kirigami.ColumnView.preferredWidth: Kirigami.Units.gridUnit * 25
    Kirigami.ColumnView.maximumWidth: Kirigami.Units.gridUnit * 50

    ColumnLayout {
        Controls.Label {
            text: "Label"
        }

        Controls.Label {
            enabled: false
            text: "Disabled Label"
        }

        Kirigami.Heading {
            level: 1
            text: "Heading"
        }

        Kirigami.Heading {
            enabled: false
            level: 1
            text: "Disabled Heading"
        }

        Controls.TextField {
            placeholderText: "Text Field"
        }

        Controls.TextField {
            enabled: false
            placeholderText: "Disabled Text Field"
        }

        Kirigami.PasswordField {
            placeholderText: "Password field"
        }

        Controls.TextArea {
            placeholderText: "Text Area"
        }

        Controls.TextArea {
            enabled: false
            placeholderText: "Disabled Text Area"
        }

        Controls.SpinBox {
            from: 0
            to: 1000
            stepSize: 1
        }

        Controls.SpinBox {
            enabled: false
            from: 0
            to: 1000
            stepSize: 1
        }

        Controls.SpinBox {
            Layout.preferredWidth: 50
            from: 10
            to: 1000
            stepSize: 10
        }


        ListModel {
            id : fruitModel
            ListElement { name: "Apple"; color: "green" }
            ListElement { name: "Cherry"; color: "red" }
            ListElement { name: "Banana"; color: "yellow" }
            ListElement { name: "Orange"; color: "orange" }
            ListElement { name: "WaterMelon"; color: "pink" }
        }

        SortFilterProxyModel {
            id: fruitFilter
            model: fruitModel
            sorters: [
                RoleSorter {
                    roleName: "name"
                }
            ]
            filters: [
                FunctionFilter {
                    component CustomData: QtObject { property string name }
                    property var regExp: new RegExp(fruitSearch.text, "i")
                    onRegExpChanged: invalidate()
                    function filter(data: CustomData): bool {
                        return regExp.test(data.name);
                    }
                }
            ]
        }

        Controls.SearchField {
            Layout.fillWidth: true
            id: fruitSearch
            suggestionModel: fruitFilter
            textRole: "name"
        }

        Item {
            Layout.fillHeight: true
        }
    }
}
